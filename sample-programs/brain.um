# Sample Program: brain.um

fnc inc() {
  if memory[ptr] == 255 {
    memory[ptr] = 0;
    return;
  }

  memory[ptr] += 1;
}

fnc dec() {
  if memory[ptr] == 0 {
    memory[ptr] = 255;
    return;
  }

  memory[ptr] -= 1;
}

fnc right() {
  if ptr == 30000 {
    ptr = 0;
    return;
  }

  ptr += 1;
}

fnc left() {
  if ptr == 0 {
    ptr = 30000;
    return;
  }

  ptr -= 1;
}

fnc write_char() {
  let byte = memory[ptr];
  let c = chr(byte);
  printx(c);
}

var memory = [];
let mem_size = 100;
var i = mem_size;

loop {
  i -= 1;
  if i < 0 {
    break;
  }
  memory[i] = 0;
}

var depth = 0;
var ptr = 0;

var pc = 0;

fnc brain(code: String) {
  let count = len(code);

  loop {
    if pc >= count {
      break;
    }

    let c = code[pc];
    pc += 1;

    if c == "+" {
      inc();
    }
    if c == "-" {
      dec();
    }
    if c == "<" {
      left();
    }
    if c == ">" {
      right();
    }
    if c == "." {
      write_char();
    }
    if c == "," {
      # TODO
    }
    if c == "[" {
      if memory[ptr] != 0 {
        continue;
      }

      let start_depth = depth;
      loop {
        if pc >= count {
          print("ran out of instructions");
          break;
        }

        if code[pc] == "[" {
          depth += 1;
        }
        if code[pc] == "]" {
          depth -= 1;
          if depth == start_depth {
            break;
          }
        }

        pc += 1;
      }
    }
    if c == "]" {
      if memory[ptr] == 0 {
        continue;
      }

      let start_depth = depth;
      loop {
        pc -= 1;
        if pc < 0 {
          print("ran out of instructions");
          break;
        }

        if code[pc] == "]" {
          depth += 1;
        }
        if code[pc] == "[" {
          depth -= 1;
          if depth == start_depth {
            pc += 1;
            break;
          }
        }
      }
    }
  }
}

let hello = "++++++++[>++++[>++>+++>+++>+<<<<-]>+>+>->>+[<]<-]>>.>---.+++++++..+++.>>.<-.<.+++.------.--------.>>+.>++.";

brain(hello);